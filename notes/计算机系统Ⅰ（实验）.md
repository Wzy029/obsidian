====## 数值系统
0：低电平/False
1：高电平/True
x/X：电平未知
z/Z：高阻态（接近于没有输出）

## 数字表示
```verilog
### <bits>'<radix><value>

bits：二进制位宽
radix：进制，十进制（d/D）、十六进制（h/H）、二进制（b/B）、八进制（o/O）
value：实际数值（可以加入下划线提高可读性）
	如：-4'hf=-4'd15=-15; 9'o210=9'b010_001_000='b0_1000_1000=136
	* 十六进制中不分大小写
```

## 标识符与变量

```verilog

wire
	声明线网型数据，默认初始值为z。verilog的默认数据类型
	本质为导线
reg
	声明在always语句内部进行赋值操作的信号。一般对应一种存储单元，默认初始值为x。
	*在always语句内部被赋值的信号，都应该被定义成reg

声明变量时，一般用如下格式：
	wire/reg [width-1:0] (<var_name>,...) ;
		width-x:位宽,0:从0开始
		如：wire [4:0] my_vec代表一个位宽为5的向量my_vec，范围是0-4
		数组索引从高到低！
		my_vec[0] // 表示最低位
		my_vec[3:2]// 表示第四、三位
		my_vec[4]// 表示最高位，值为 1'b0

也可以声明数组：
	wire/reg [width-1:0] <var_name> [0:width-1];
		reg [7:0] my_regs [0:31];
			//声明了一个数组，该数组由 32 个位宽为 8 的 reg 型变量组成。
```

### 运算符

#### 算数运算符
	+、-、*、/、%

#### 关系运算符
	>、<、>=、<=、==、!=、===（全等）、!==（非全等）
	运算结果固定为 0（False）或 1（True），是一个 1bit 的值。
	如果某一个操作数中*有一位为 x 或 z*，则前六种关系运算的结果为 x。
	全等比较则对为 x 或 z 的位也进行比较，只有两个操作数完全一致时，其结果才是 1

#### 逻辑运算符
	&&：与，等价于A*B
	||：或，等价于A+B
	！：非，取反
	如果一个操作数不为 0，则在逻辑运算时它等价于逻辑 1
	如果一个操作数等于 0，则它运算时等价于逻辑 0
	如果它任意一位为 x 或 z，则它等价于 x

#### 按位运算符
	~ 按位非
	& 按位与
	| 按位或
	^ 按位异或：不同为1
	~^或^~ 按位同或：不同为0

#### 归约运算符
	对一个数的每一位做处理，输出1bit结果

#### 移位运算符
	<<:逻辑左移，右边低位补0
	>>：逻辑右移，左边高位补0
	<<<：算数左移，右边低位补0
	>>>：算数右移，左边高位补符号位

#### 拼接运算符
	{表达式 1，表达式 2,....,表达式 N}
	与拼接操作经常一起使用的是重复操作，其一般格式是：重复次数{表达式}
	a[7:4]={a[0], a[1] ,a[2], a[3]};
		（a[7]=a[0],a[6]=a[1],a[5]=a[2],a[4]=a[3])
	b[31:0]={{24{a[7]}}, a[7:0]};

### Verilog语句

#### 连续赋值：assign
```verilog
assign LHS=RHS;//LHS必须是wire型

	实例：
	wire Cout,A,B;//define
	assign Cout=A&B;

	(等价于)
	wire A,B;
	wire Cout=A&B;
**只要 RHS 表达式的操作数有事件发生（值的变化）时，RHS 就会立刻重新计算，同时赋值给 LHS**
```

#### 过程赋值：always、initial
```verilog
用于对reg型变量赋值
always和initial不可嵌套，彼此同时执行。
always会一直循环，initial只执行一次

always引入了敏感变量的概念
	always @(敏感变量列表) 过程语句
		// 每当 a 或 b 的值发生变化时就执行内部的语句
		always @(a or b) begin
		    [过程语句]
		end
%% 	允许使用符号 `*` 在敏感列表中表示缺省，编译器会根据 always 块内部的内容自动识别敏感变量 %%
	reg Cout;
	wire A, B;
	always @(*) begin
	    Cout = A & B;
	end
	Verilog 还支持通过使用 `posedge` 和 `negedge` 关键字将电平变化作为敏感变量。
	其中 `posedge` 对应上升沿，`negedge` 对应下降沿。
	例如：下面的代码仅在 clk 从低电平（0）变为高电平（1）时触发。
	reg Cout;
	wire A, B, clk;
	always @(posedge clk) begin
	    Cout <= A & B;
	end
```

#### if-else
	只能在always内使用，**if-else 是二路选择器，不是 C 语言的表达式**。
```verilog
reg O; 
always @(*) begin 
	if (S0) O <= I[1]; 
	else O <= I[0]; 
end
```

#### case
```verilog
module Mux4To1( 
	input [3:0] I, 
	input [1:0] S, 
	output reg O 
); 
	always @(*) begin 
		case (S) 
			2'b00: O <= I[0]; 
			2'b01: O <= I[1]; 
			2'b10: O <= I[2]; 
			2'b11: O <= I[3]; 
		endcase 
	end 
	
endmodule
```


> [!warning] WARNING
> if 语句要有完整的 if-else
case 语句要遍历所有的 case 硬件码，或者提供 default


> [!NOTE] <=非阻塞赋值
> ```verilog
> module example ( 
> 	input clk, 
> 	input [3:0] I, 
> 	output reg [3:0] O
>  ); 
> 	 always @(posedge clk) begin 
> 		 O <= I[0]; // 非阻塞赋值 
> 	 end 
> endmodule
> 时钟周期结束后才会更新，不同于=（阻塞赋值）即时更新
```
```

#### generate语句
```verilog
wire [3:0] a [3:0];
wire [3:0] b [3:0];

genvar i;//定义仅用于generate语句内部的变量
generate
    for(i=0;i<4;i=i+1)begin
        assign b[i] = a[i];
    end
endgenerate
```

#### integer变量
32位的变量，等于32bit的reg
16位变量：short，64位变量：longint
```verilog
integer i;
initial begin
    ...
    for(i=0;i<8;i=i+1)begin
        data=i[3:0];
        #5;
    end
    ...
end
```

### 参数化编程
#### 宏变量参数
方便修改
```verilog
`define LEN 4
wire [3:0] a [`LEN-1:0];
wire [3:0] b [`LEN-1:0];

genvar i;
generate
    for(i=0;i<`LEN;i=i+1)begin
        assign b[i] = a[i];
    end
endgenerate
```

#### localparam本地参数
类似const
只能定义在模块内部，仅对本模块有用
```verilog
localparam LEN=4;
wire [3:0] a [LEN-1:0];
wire [3:0] b [LEN-1:0];

genvar i;
generate
    for(i=0;i<LEN;i=i+1)begin
        assign b[i] = a[i];
    end
endgenerate
```

#### parameter变量
可修改覆盖原来的值
```verilog
module dummy #(
    parameter LEN = 4 //可以是多个，用‘，’隔开
) (
    input ...
    output ...
);
    ...
    wire [3:0] a [LEN-1:0];
    wire [3:0] b [LEN-1:0];

    genvar i;
    generate
        for(i=0;i<LEN;i=i+1)begin
            assign b[i] = a[i];
        end
    endgenerate
    ...
endmodule

dummy dummy1 (
    ...
);// LEN = 4

dummy #(
    .LEN(5)
) dummy2 (
    ...
);
```

### 多路选择器

	包括输入信号 I0,I1,I2,⋯,IN，选择信号 S0,S1,⋯,SlogN，输出信号 O。当输入信号 S={SlogN,⋯,S1,S0}等于 i 时，输出信号 O=Ii。
#### 二路选择器
![[Pasted image 20250226182442.png|182]]
	电路图：
    ![[Pasted image 20250226182503.png|450]]
```verilog
module Mux2To1(
	input I0,
	input I1,
	input S0,
	output O
);
	assign O = S0 ? I1 : I0 ; 
endmodule
```

> [!NOTE] ? :
> 输入（S0）为1，判断为真，输出I1，反之I0

#### 四路选择器
![[Pasted image 20250226183255.png|400]]
	电路图：
	![[Pasted image 20250226183349.png|425]]


```verilog
module Mux2To1(
	input I0,
	input I1,
	input S0,
	output O
);
	assign O = I1 & S0 | I0 & ~S0 ;
endmodule

module Mux4To1(
	input [3:0] I,
	input [1:0] S,
	output O
);
	wire [1:0] I_S;

	Mux2To1 mux0(I[0],I[1],S[0],I_S[0]); 
	Mux2To1 mux1(I[2],I[3],S[0],I_S[1]); 
	Mux2To1 mux2(I_S[0],I_S[1],S[1],O);
	
endmodule
```
> [!NOTE] 一维数组
> ```verilog
wire/input...  [a:b] I
> 如： wire [5:7] I;  //I[5], I[6], I[7]
> 	assign a[2:4]=b[7:5];   //a[2] 连接 b[7]，a[3] 连接 b[6]，a[4] 连接 b[5]


> [!NOTE] 二维数组
> ```verilog
> wire [num2 : num1] a [num3 : num4];
> 共有|num4-num3|+1组线路，每组有|num2-num1|根线路
> a[i]表示其中一组线路，a[i:j]表示a[i]-a[j]组线路总和

#### index索引

exp1
```verilog
wire [3:0] I; 
wire [1:0] S;  //S为一个两位宽的数（00，01，10，11），即0-3
wire [1:0] O; 
assign O[0] = I[1]; 
assign O[1] = I[S]; //若S=1，则O[1]=I[1]，以此类推
```

exp2
```verilog
module LUT3( 
	input [2:0] S,//S=0~8
	output O 
); 
	wire [7:0] array; 
	assign array = 8'b10010110; 
	assign O = array[S]; 

endmodule
```


### 变量译码器（decoder）
	译码器（decoder）：多输入多输出组合逻辑电路器件，可分为变量译码和显示译码两类
		广义的译码器是指将二进制编码输入转换为任意的编码输出，且这个转换关系可以不满足单射。
	变量译码器：较少输入→较多输出，常见的如n线-2^n线译码、8421BCD码译码
	显示译码器：将二进制数转换成对应的七段码，一般可分为驱动LED和驱动LCD

### 复合多路选择器
	多路选择器：从单bit中选取单bit输出；复合多路选择器：从多组输入中选取单组输出	
#### 实现方式
##### 多个一位多路选择器组合
```verilog
module Mux4T1_4(
	input [3:0]I0,
	input [3:0]I1,
	input [3:0]I2,
	input [3:0]I3,
	input [1:0]S,
	output [3:0]O
);

	Mux4T1_1({I3[0],I2[0],I1[0],I0[0]},S,O[0]);
	Mux4T1_1({I3[1],I2[1],I1[1],I0[1]},S,O[1]);
	Mux4T1_1({I3[2],I2[2],I1[2],I0[2]},S,O[2]);
	Mux4T1_1({I3[3],I2[3],I1[3],I0[3]},S,O[3]);

endmodule
```

> [!info] { }的作用
> 将里面的单位数据组合成多位数据输入
> - `LEN({ W0, W1, W2, ..., Wn }) = Sum(LEN(W0), LEN(W1), ..., LEN(Wn))`

##### 与或形式
```verilog
module Mux4T1_4(
	input [3:0] I0, 
	input [3:0] I1, 
	input [3:0] I2, 
	input [3:0] I3, 
	input [1:0] S, 
	output reg [3:0] O 
); 
	wire [3:0] sel; 
	assign sel[0] = ~S[0]&~S[1]; 
	assign sel[1] = S[0]&~S[1]; 
	assign sel[2] = ~S[0]&S[1]; 
	assign sel[3] = S[0]&S[1]; 
	assign O = {4{sel[0]}}&I0 | {4{sel[1]}}&I1 | {4{sel[2]}}&I2 | {4{sel[3]}}&I3; 

endmodule
```

##### ?: 语法
```verilog
module Mux4T1_4(
    input [3:0] I0,
    input [3:0] I1,
    input [3:0] I2,
    input [3:0] I3,
    input [1:0] S,
    output [3:0] O
);

    assign O=S[1] ? (S[0] ? I3 : I2) : (S[0] ? I1 : I0);

endmodule
```

##### index 索引
```verilog
module Mux4T1_4(
    input [3:0] I0,
    input [3:0] I1,
    input [3:0] I2,
    input [3:0] I3,
    input [1:0] S,
    output [3:0] O
);

    wire [3:0] I [3:0];
    assign I[0] = I0;
    assign I[1] = I1;
    assign I[2] = I2;
    assign I[3] = I3;

    assign O = I[S];

endmodule
```

##### if-else 语法
```verilog
module Mux4T1_4(
    input [3:0] I0,
    input [3:0] I1,
    input [3:0] I2,
    input [3:0] I3,
    input [1:0] S,
    output reg [3:0] O
);

    always @(*) begin
        if (S[1]) begin
            if (S[0]) O <= I3;
            else O <= I2;
        end else begin
            if (S[0]) O <= I1;
            else O <= I0;
        end      
    end 

endmodule
```

##### case语法
```verilog
module Mux4T1_4(
    input [3:0] I0,
    input [3:0] I1,
    input [3:0] I2,
    input [3:0] I3,
    input [1:0] S,
    output reg [3:0] O
);

    always @(*) begin
        case (S)
            2'b00 : O <= I0;
            2'b01 : O <= I1;
            2'b10 : O <= I2;
            2'b11 : O <= I3;
        endcase
    end 

endmodule
```

### 七段数码管
	数码管：七段数码管、八段数码管（多一个用于显示小数点的发光二极管单元DP（decimal point）
	七段数码管：共阳极&共阴极，共阳极的七段数码管的正极（或阳极）为八个发光二极管的共有正极，正极接电，负极接地

> [!tip] Nexys A7
>  [Nexys A7](https://digilent.com/reference/programmable-logic/nexys-a7/start) 开发板的七段数码管为**共阳极**数码管，当输入信号为 0 时，对应数码管亮；输入信号为 1 时，对应数码管灭。

![[Pasted image 20250301142425.png|500]]
显示译码的对应关系：
![[Pasted image 20250301142458.png|159]]

### 动态时分复用
	Nexys-A7 开发板有八个七段数码管，所有七段数码管共用 CA、CB ... DP 的输出。此外每个七段数码管有自己专用的 AN 信号线作为使能信号，当 AN=1 时，七段数码管无论 CA、CB ... DP 输入为多少，七段数码管不亮；**当 AN=0 时七段数码管才根据 CA、CB ... DP 的输入亮灭**。


> [!example] 让八个七段数码管分别显示 32'h12345678
> 将一个时间周期平均分为八分
> 
> 在第一分时间内我们将 4'h1 的译码结果输出到七段数码管，然后将第一个七段数码管的 AN 启动，其余七段数码管的 AN 关闭，这样第一个七段数码管会显示数字 1，其余数码管不亮。
> 
> 第二个时间片输出 4'h2 的译码结果，仅使能第二个七段数码管，这样只有第二个七段数码管显示数字 2，其余数码管不亮。
> 
> 以此类推，最后每个数码管都会有八分之一的时间显示自己对应的数据，然后因为视觉残留，最后人眼会感觉八个数码管同时亮起，且分别显示对应的数据。
> 

## Lab3-1_有限状态机
	寄存器组+组合逻辑

- 状态寄存器由**一组触发器**组成，用来记忆状态机当前所处的状态，状态的改变只发生在**时钟的跳变沿**
- 状态是否/如何改变，取决于**组合逻辑F**的输出，F为**当前状态和输入信号的函数**
- 状态机的**输出**是由**输出组合逻辑G**提供的，G也是**当前状态和输入信号的函数**
### Mealy型状态机
	输出与当前状态、输入都有关

![[Pasted image 20250326175555.png|450]]

### Moore型状态机
	输出只与当前状态有关

![[Pasted image 20250326175957.png|450]]

### 设计步骤
- 系统架构和接口定义：

|  接口   | 接口定义 |
| :---: | ---- |
|  **clk**  | 系统时钟 |
| **rst_n** | 系统复位 |
|   **X**   | 序列输入 |
|   **Y**   | 检测输出 |

- 状态定义和编码
1. 二进制码（binary）
2. 格雷码（gray）：在相邻状态跳转时，只有单比特翻转，功耗低
3. 独热码（one hot）：增加了两个寄存器，但更节省组合逻辑电路，因为只要比较一个比特位

|     | 二进制 | 格雷码 | 独热码  |
| :-: | :-: | :-: | :--: |
| S0  | 00  | 00  | 0001 |
| S1  | 01  | 01  | 0010 |
| S2  | 10  | 11  | 0100 |
| S3  | 11  | 10  | 1000 |
#### 三段式设计方法
	将输出信号于状态跳转分开描述，且状态跳转用组合逻辑来控制

1. 给出**状态转换图**理清时序
例如：1110010
![[Pasted image 20250326184444.png|500]]
每个箭头的指向，都能保证从断点开始，Y=1会在1110010后输出

2. 三段式写法
```verilog
module seq(
    input clk,
    input reset,
    input in,
    output out
    );

    localparam [2:0] S0 = 3'b000;
    localparam [2:0] S1 = 3'b001;
    localparam [2:0] S2 = 3'b010;
    localparam [2:0] S3 = 3'b011;
    localparam [2:0] S4 = 3'b100;
    localparam [2:0] S5 = 3'b101;
    localparam [2:0] S6 = 3'b110;
    localparam [2:0] S7 = 3'b111;  

    reg [2:0] curr_state, next_state;
    reg out_reg;

//第一段：传递寄存器状态，时序逻辑，非阻塞赋值
    always @(posedge clk or negedge reset) begin//在clk的上升沿或reset下降沿启动
        if(!reset)
            curr_state <= S0; //reset下降沿，重置
        else
            curr_state <= next_state;
    end

//第二段：根据当前状态和当前输入确定下一状态，组合逻辑，阻塞赋值
    always @(*) begin
        if(!reset)
            next_state = S0; //reset=0，重置
        else begin
            case(curr_state)
                S0: begin 
                    if(in == 0) next_state = S0;
                    else next_state = S1;
                end
                S1: begin 
                    if(in == 0) next_state = S0;
                    else next_state = S2;
                end
                S2: begin 
                    if(in == 0) next_state = S0;
                    else next_state = S3;
                end
                S3: begin 
                    if(in == 0) next_state = S4;
                    else next_state = S3;
                end
                S4: begin 
                    if(in == 0) next_state = S5;
                    else next_state = S1;
                end
                S5: begin 
                    if(in == 0) next_state = S0;
                    else next_state = S6;
                end
                S6: begin 
                    if(in == 0) next_state = S7;
                    else next_state = S2;
                end
                S7: begin 
                    if(in == 0) next_state = S0;
                    else next_state = S1;
                end
                default: next_state = S0;
            endcase
        end
    end

//第三段：根据当前状态和当前输入确定输出，时序逻辑，非阻塞赋值
    always @(posedge clk or negedge reset) begin
        if(!reset)
            out_reg <= 0;
        else
            out_reg <= (curr_state == S7)?1:0;
    end
    assign out = out_reg;

endmodule
```

3. enum枚举

| S2  | S1  | S0  |  X  | S2  | S1  | S0  |  Y  |
| :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: |
|  0  |  0  |  0  |  0  |  0  |  0  |  0  |  0  |
|  0  |  0  |  0  |  1  |  0  |  0  |  1  |  0  |
|  0  |  0  |  1  |  0  |  0  |  0  |  0  |  0  |
|  0  |  0  |  1  |  1  |  0  |  1  |  0  |  0  |
|  0  |  1  |  0  |  0  |  0  |  0  |  0  |  0  |
|  0  |  1  |  0  |  1  |  1  |  0  |  0  |  0  |
|  0  |  1  |  1  |  0  |  1  |  0  |  0  |  0  |
|  0  |  1  |  1  |  1  |  0  |  1  |  1  |  0  |
|  1  |  0  |  0  |  0  |  1  |  0  |  1  |  0  |
|  1  |  0  |  0  |  1  |  0  |  0  |  1  |  0  |
|  1  |  0  |  1  |  0  |  0  |  0  |  0  |  0  |
|  1  |  0  |  1  |  1  |  1  |  1  |  0  |  0  |
|  1  |  1  |  0  |  0  |  1  |  1  |  1  |  1  |
|  1  |  1  |  0  |  1  |  0  |  1  |  0  |  0  |
|  1  |  1  |  1  |  0  |  0  |  0  |  0  |  0  |
|  1  |  1  |  1  |  1  |  0  |  0  |  1  |  0  |
*Y是下个周期的Y值*
```verilog
typedef enum logic [2:0] {S0,S1,S2,S3,S4,S5,S6,S7} fsm_state;

fsm_state state;
always@(posedge clk or negedge rstn)begin
    if(~rstn)begin
        state <= S0;
    end else begin
        case(state)
            S0:if(X) state<=S1; else state<=S0; 
            S1:if(X) state<=S2; else state<=S0;
            S2:if(X) state<=S3; else state<=S0;
            S3:if(X) state<=S3; else state<=S4;
            S4:if(X) state<=S1; else state<=S5;
            S5:if(X) state<=S6; else state<=S0;
            S6:if(X) state<=S2; else state<=S7;
            S7:if(X) state<=S1; else state<=S0;
        endcase
    end
end

assign Y = state == S7;
```

4. 译码器范式
	多路选择器
```verilog
reg [2:0] state;
always@(posedge clk or negedge rstn)begin
    if(~rstn)begin
        state <= 3'b000;
    end else begin
        case({state, X})
            4'b0000:{state,Y} <= 4'b0000;
            4'b0001:{state,Y} <= 4'b0010;
            4'b0010:{state,Y} <= 4'b0000;
            4'b0011:{state,Y} <= 4'b0100;
            4'b0100:{state,Y} <= 4'b0000;
            4'b0101:{state,Y} <= 4'b1000;
            4'b0110:{state,Y} <= 4'b1000;
            4'b0111:{state,Y} <= 4'b0110;
            4'b1000:{state,Y} <= 4'b0010;
            4'b1001:{state,Y} <= 4'b0010;
            4'b1010:{state,Y} <= 4'b0000;
            4'b1011:{state,Y} <= 4'b1100;
            4'b1100:{state,Y} <= 4'b1111;
            4'b1101:{state,Y} <= 4'b0100;
            4'b1110:{state,Y} <= 4'b0000;
            4'b1111:{state,Y} <= 4'b0010;
        endcase
    end
end
```
等价于
```verilog
wire [3:0] table [15:0];
assign table[0]  = 4'b0000;
assign table[1]  = 4'b0010;
assign table[2]  = 4'b0000;
assign table[3]  = 4'b0100;
assign table[4]  = 4'b0000;
assign table[5]  = 4'b1000;
assign table[6]  = 4'b1000;
assign table[7]  = 4'b0110;
assign table[8]  = 4'b0010;
assign table[9]  = 4'b0010;
assign table[10] = 4'b0000;
assign table[11] = 4'b1100;
assign table[12] = 4'b1111;
assign table[13] = 4'b0100;
assign table[14] = 4'b0000;
assign table[15] = 4'b0010;

always@(posedge clk or negedge rstn)begin
    if(~rstn)begin
        state <= 3'b000;
    end else begin
        {state, Y} <= table[{state, X}];
    end
end
```

5. 查表法
- 用寄存器构造表格
- 先将数据存入一个hex文件
```verilog
0
2
0
4
0
8
8
6
2
2
0
c
f
4
0
2
```
- 然后用$readmemh用hex的内容初始化寄存器数组
```verilog
reg [3:0] table [15:0];
always@(posedge clk or negedge rstn)begin
    if(~rstn)begin
        state <= 3'b000;
    end else begin
        {state, Y} <= table[{state, X}];
    end
end

initial begin
    $readmemh("table.hex", table);
end
```

## Lab3-2_计数器
	1. 二进制计数器
	2. BCD计数器
- **工作原理**：基于时钟信号的**上升沿或下降沿**触发计数器计数功能。如：一个上升沿触发器被连接到计数器的时钟输入端时，上升沿来时计数器+1，计数达到最大值时，自动复位为0
- 可用于**产生周期性的脉冲信号**/实现**分频器**和其他数字电路
- 广义的计数器是**对状态进行有序遍历的时序电路**，也称**模N计数器**，其功能是反复遍历N个固定状态的计数器（在M个状态中选N个遍历）

> [!example] 一个模6计数器的状态图
> ![[Pasted image 20250327105309.png|200]]

```verilog
module counter_mod6(clk, reset, out);
    input wire clk;
    input wire reset;
    output wire [2:0] out;

    reg [2:0] cnt;

    always @(posedge clk) begin
        if (reset) begin
            cnt <= 0;
        end
        else begin
	        case(out)
	            3'b000: cnt <= 3'b001;
	            3'b001: cnt <= 3'b010;
	            3'b010: cnt <= 3'b011;
	            3'b011: cnt <= 3'b100;
	            3'b100: cnt <= 3'b101;
	            3'b101: cnt <= 3'b000;
	            default: cnt <= cnt + 1;
	        endcase
        end
    end

    assign out = cnt;
endmodule
```

always模块也可以用一下加法函数代替
```verilog
always @(posedge clk) begin
    if (reset) begin
        cnt <= 0;
    end else if(cnt == 3'd5)begin//满了复位，注意是==，用<=的话开销比较大
        cnt <= 0;
    end else begin
        cnt <= cnt + 3'd1;
    end
end
```

### BCD码计数器
- BCD码：用4位二进制来表示十进制的每一位
> [!example] 34
> 二进制原码：00100010，BCD码：0011 0100
- 可以将一个mod 10的二进制计数器和一个mod 6的二进制计数器组合得到60 BCD码计数器
   ![[Pasted image 20250327110525.png|350]]
	- co：co=1 表示计数器进位，用于通知外部设备进位事件的发生
	- low_co：高位计数器用于接收低位计数器的进位信息，如果low_co=1，高位计数器递增（表示mod 6计数满，开始计mod 10）
	- en：使能，en=1时计数器工作

## Lab3-3 乘法器（Multiplier）
手工运算的执行过程：
![[Pasted image 20250407173440.png|250]]
### 硬件实现
- 判断：y的最低位是0还是1
	- 1：把x加到部分积上
	- 0：若为0，保持原值
- x左移一位，y右移一位
- 判断：y为0？
	- 循环结束
	- 否，继续循环
![[Pasted image 20250407173801.png|400]]
![[Pasted image 20250407173917.png|425]]
### 乘法器的优化实现
![[Pasted image 20250407174055.png|425]]
```verilog
high_product = 0;
low_product = multiplier;
// 初始化阶段，product 的高 64 位载入 0
// product 的低 64 位载入乘数

for(i=0;i<64;i++){
    // 计算阶段，需要经历 64 次迭代
    if(low_product & 1){
        high_product_tmp = high_product + multiplicant;
    }else{
        high_product_tmp = high_product;
    }
    // 参考列竖式的计算方式，每次迭代都做一次加法，不要忘记加法的进位
    {high_product, low_product} = {high_product_tmp, low_product} >> 1;
    // 加法的结果右移，等价于被乘数左移，起到进位的作用
    // 乘数的右移既是为了给加法的右移提供空间
    // 空余多得到的 1 bit 可以存放加法的进位
    // 下次迭代要参考的乘数的 0/1 bit 可以在 low_product 的最低位
}

return {high_product, low_product};
// 结束阶段，返回最终结果
```

### 状态机设计
- 初始状态（idle）：当模块被使能时，将被乘数、乘数载入对应的寄存器，进入工作状态开始执行迭代
- 工作状态（work）：执行加法、根据乘数的bit值选择是用加法结果还是载入原值、计算结果移位后载入寄存器、计数器迭代，迭代到次数了进入结束状态
- 结束状态（final）：输出最终结果，并通知调用方接收结果
![[Pasted image 20250407175059.png|475]]
### 各阶段电路设计
#### 初始化阶段
- multiplicant寄存器载入输入的被乘数
	![[Pasted image 20250409123520.png|113]]
- product寄存器载入multiplier和0
	![[Pasted image 20250409123540.png|200]]
#### 工作阶段
- multiplicant保持不变
- product载入加法运算、选择运算、移位运算后的结果
![[Pasted image 20250409123706.png|325]]
#### 结束阶段
- multiplicant保持不变
- product保持不变

### 状态机控制寄存器
每个寄存器需要的计算电路和载入的结果都略有不同，所以这里的每一套计算电路都要实现，然后根据状态机的状态将对应的计算电路的计算结果用选择器选择出来，载入给对应的寄存器
![[Pasted image 20250409124026.png|400]]
## start-finish握手信号
处理器有时需要使用乘法器进行乘法操作，因此处理器需要信号通知乘法器单元接收运算内容、开始乘法运算，当乘法器单元执行结束之后，乘法器需要信号通知处理器计算完毕、接受运算结果
所以可以定义简单的start-finish握手协议：
- 调用模块（caller）向被调用模块（callee）发送：
	要被执行的数据 & 至少持续一个周期的start信号，当start=1时发送的数据有效；
	callee发现start=1必须立刻接受数据
- callee执行结束后向caller发送：
	执行结果 & 至少持续一个周期的finish信号，finish=1的时候发送的数据有效
	被caller发现finish=1必须立刻接收callee的数据
- 当start请求被发送而finish请求没有反馈之前，不能发送下一个start
	caller只有收到finish后才可以发送下一个start
- 当一个start没被发送，caller不能发送finish

其中caller发送给callee的< 数据和start >作为caller发送给callee的**请求信号(request)**
callee发送给caller的< finish >作为callee响应caller的**回应信号(resp)**
![[Pasted image 20250409124845.png|500]]


> [!question] 缺点
> caller无法知道callee是否空闲，只能等`finish=1`是才再次使能`start`信号，确保callee一定空闲。
> 这使得caller在`start--finish`期间只能执行一次callee调用
> 但callee也许可以连续执行多次caller调用，因此caller期望callee可以提供一个信号来表示自己是否空闲
> 
> 因此caller期望向callee提供一个信号来表示自己是否准备就绪，防止callee在caller接受之前无效的返回结果——valid-ready协议


---
## Lab4-1 卷积模块
### 卷积操作
#### 基本概念
通过两个函数f和g生成第三个函数
特殊的积分变换
f和g经过翻转和平移，重叠部分函数值<乘积>，对重叠<长度>的<积分>

- 卷积核大小（Kernel Size）：卷积核大小定义了卷积的视野。2维中的常见选择是3（3×3像素矩阵）
- 步长（Stride）：步长定义遍历图像时卷积核的移动的步长。默认值为1，可以调整
- 填充（Padding）：填充定义如何处理样本的边界。保持卷积操作的输出尺寸=输入尺寸（如果卷积核>1，不加padding会导致输出尺寸小于输入）
- 输入和输出通道（Channels）：卷积层通常需要一定数量的输入通道（I）并计算一定数量的输出通道（O）。通过I\*O\*K来计算（K是卷积核大小）
#### 一维卷积
假设卷积核长$k$，对应的权重分别是$[a_0, a_1, ..., a_{k-1}]$
要处理的数据长为$m$，对应的值分别是$[b_0, b_1,..., b_{m-1}]$
那么得到的结果长为$m-k-1$，对应的值分别是$[c_0, c_1,..., c_{m-k}]$
其中，$c_i = a_0b_i+a_1b_{i+1}+...+a_{k-1}b_{i+k-1}$


> [!example] 一维卷积
> 例如有一组电路会对某个输入进行采样，信道稳定的情况下可以得到数据信号`100, 105, 103, 102, 107`。但是因为信道扰动，随后得到的是`100, 105, 130, 103, 102, 107`，存在明显异常。因此采样电路需要一些策略来减少异常值的影响。
> 
> 策略：将采样的值和相邻的两个数据取平均作为最后的结果，对于第一个数和最后一个数（缺少相邻值）前后补0。然后1/3加权求和
> ![[Pasted image 20250417102443.png|300]]
> 然后继续下一个采样
> ![[Pasted image 20250417102546.png|300]]
> 最后得到结果
> ![[Pasted image 20250417102607.png|300]]
> 可以看到中间部分的数据值都变得很相近了，所以这里可以通过电路平滑采样数据，消除边缘奇异点的影响。
> 采样数据序列就是要被卷积的对象，`(1/3,1\3,1\3)`的加权求和的权重就是卷积核，加权求和的过程就是卷积运算的过程，边缘填充的0就是padding，处理后的结果就是卷积操作的结果，卷积步长为1，输入输出通道为1

用不同的卷积核可以得到不同的数据后处理效果，例如模糊、锐化、求导等


> [!example] 
> 卷积核`(-2, 2, 1)`，输入通道1，输出通道1，步长1，无数据填充
> ![[Pasted image 20250417112504.png|300]]

### 移位寄存器
移位寄存器序列中，每个寄存器的输入都是上一个寄存器的输出。
第一个寄存器载入新的值，其余寄存器载入上一个寄存器的值。
![[Pasted image 20250417112655.png|400]]
位宽为1的移位寄存器组：
```verilog
	wire in_data;
	reg [3:0] left_shift_reg;
	reg[3:0] right_shift_reg;
	always@(posedge clk)begin
		if(en)begin
			left_shift_reg<={left_shift_reg[2:0], in_data};
			right_shift_reg<={in_data, right_shift_reg[3:1]};
		end
	end
```

如果是向量寄存器的移位寄存器组，则要用`for`循环等语法实现
```verilog
    wire [3:0] in_data;
    reg [3:0] left_shift_reg [3:0];
    reg [3:0] right_shift_reg [3:0];

    integer i;
    always@(posedge clk)begin
        if(en)begin
            for(i=0;i<3;i=i+1)begin
                left_shift_reg[i+1] <= left_shift_reg[i];
            end
            left_shift_reg[0] <= in_data;
        end
    end
    
    always@(posedge clk)begin
        if(en)begin
            for(i=0;i<3;i=i+1)begin
                right_shift_reg[i] <= right_shift_reg[i+1];
            end
            right_shift_reg[3] <= in_data;
        end
    end
```

### valid-ready协议
- 对于任何需要被发送的数据组合，发送方提供一个`valid`信号，接受方提供一个`ready`信号
- 发送方：准备就绪时设置`valid=1`
- 接收方：准备就绪时设置`ready=1`
- `valid=1`且`ready=1`时，本次握手正式完成，在下个`posedge clk`接受方就会载入发送方放置在数据线上的数据。
	- 握手完成时，**接收方**必须立刻接收，如果无法继续接收/不想继续接收，设置`ready=0`，如果还能接收/愿意接收，保持`ready=1`
	- 握手完成时，**发送方**本次发送的数据已经被接收，如果不打算继续发送则设置`valid=0`；如果想继续发送则要立刻撤换数据线上的数据，否则接受方会再次接收原来的数据请求重复处理

握手成功时的时序图：（红线代表握手成功）
![[Pasted image 20250417121427.png|450]]![[Pasted image 20250417121429.png|450]]
![[Pasted image 20250417121431.png|450]]
下图连续四个周期，完成了四次握手
![[Pasted image 20250417121537.png|475]]
如果没有及时更换数据，就会被传输两次
![[Pasted image 20250417121641.png|440]]

发送方设置`valid`和接收方设置`ready`应尽量保持独立
在一些设计规则中，`ready`的设置可以等待`valid`的设置
但`valid`的设置不应该等待`ready`的设置，防止死锁

综上，start-finish转换为valid-ready
![[Pasted image 20250417122038.png|500]]
### 并行加法树
卷积操作需要将N个乘积累加，例如`(((num0 + num1) + num2) + num3)`可以用以下电路实现：
![[Pasted image 20250417132527.png|500]]
但最长路径的时延是N-1个加法器的时延
因此用结合律转换，`((num0 + num1) + (num2 + num3))`
![[Pasted image 20250417132631.png|500]]
最长路径是$log(N)$个加法器的时延
```verilog
Conv::result_t add_tmp [Conv::LEN-1:1] /* verilator split_var */;

// 下面的 add_tmp[i] = add_tmp[i*2] + add_tmp[i*2+1] 存在 add_tmp <- add_tmp 的数据依赖
// verilator 的编译器会认为存在自依赖的环形电路，从而报错
// 因此我们需要用/* verilator split_var */加以修饰，让 verilator 以 add_tmp 的每个 element 为单位做依赖分析

generate
// generate 语句除了可以有 for 循环还可以有 if-else，case-default
for(genvar i=1;i<Conv::LEN;i=i+1)begin
// genvar 可以在 for 循环内部声明，防止命名空间冲突
    if(i<Conv::LEN/2)begin
        assign add_tmp[i] = add_tmp[i*2] + add_tmp[i*2+1];
        // 加法器的结果输出作为下一级加法器得输入
        // 这些算法参考了数据结构的 heap，大家可以互为参照
    end else begin
        assign add_tmp[i] = stage1_data[(i-Conv::LEN/2)*2] + stage1_data[(i-Conv::LEN/2)*2+1]; 
        // 乘积运算结果两两相加
    end
end
endgenerate
```

## SystemVerilog高级语法
### 1 typedef
类似C语言中typedef的作用
```verilog
parameter WIDTH = 64;
parameter LEN   = 4;

typedef logic [WIDTH-1:0] data_t;
typedef logic [WIDTH*2-1:0] result_t;
```
用`data_t`代替`logic [WIDTH-1:0]`
重定义类型都是logic类型而非wire或reg

### 2 struct
原版本：
```verilog
module ConvOperator(
    input clk,
    input rst,
    input [63:0] kernel_0,
    input [63:0] kernel_1,
    input [63:0] kernel_2,
    input [63:0] kernel_3,
    input [63:0] data_0,
    input [63:0] data_1,
    input [63:0] data_2,
    input [63:0] data_3,
    input in_valid,
    output reg in_ready,

    ...
```
过于繁琐，可以用typedef和struct打包
```verilog
parameter WIDTH = 64;
parameter LEN   = 4;

typedef logic [WIDTH-1:0] data_t;
typedef logic [WIDTH*2-1:0] result_t;

typedef struct{
    data_t data [LEN-1:0];
} data_vector;
```
输入输出可以修改为：`input/output struct类型 端口名`
```verilog
module ConvOperator(
    input clk,
    input rst,
    input data_vector kernel,
    input data_vector data,
    input in_valid,
    output reg in_ready,

    ...
```

端口也可以直接用结构变量进行连接
```verilog
module m0(
    ...
    output data_vector data // 结构作为输出
);

module m1(
    ...
    input data_vector data  // 结构作为输入
);
module m3;
    data_vector data;
    m0 m0_0(
        ...
        .data(data)
    );
    m1 m1_0(
        ...
        .data(data)
    );                      // 模块间结构传递
endmodule
```

结构的成员可以是logic，logic向量，logic高维数组，枚举等。
```verilog
typedef struct {
    logic a;                // logic
    logic [3:0] b;          // logic 向量
    logic [3:0] c [3:0];    // logic 高维数组
    fsm_state state;        // 枚举
    data_vector data;       // 结构
} s0;

s0 s1;                      // 结构变量
s0 s2 [3:0];                // 结构数组
s0 s3 [3:0][3:0];           // 结构高维数组
```

结构变量直接可以直接赋值，可以用`.`访问结构的成员变量
也可以用`'{key1:value1, ... ,keyN:valueN}`直接给结构的所有成员变量对应赋值(注意有个`'`)
可以用`?:`进行选择
```verilog
typedef struct{
    logic a;
    logic [3:0] b;
} s0;
s0 s1,s2,s3,s4;
logic a1,a2;
logic [3:0] b1;

assign s1 = s2;     // 结构直接赋值
assign s2.a = a1;   // 结构成员赋值
assign s2.b = b1;
assign a2 = s1.a;   // 用结构成员给其他变量赋值
assign s3 = '{a:a1, b:b1};  // 对所有结构成员依次赋值

assign s4 = a1?s1:s2;   // 结构的选择
```
### 3 package
struct只能定义在module中，想让module的输入输出端口也使用data_vector结构/让不同模块使用相同的data_vector结构，可以使用package

```verilog
package Conv;
    parameter WIDTH = 64;
    parameter LEN   = 4;

    typedef logic [WIDTH-1:0] data_t;
    typedef logic [WIDTH*2-1:0] result_t;

    typedef struct{
        data_t data [LEN-1:0];
    } data_vector;

endpackage
```
我们在其他模块中就可以使用Conv定义的参数、类型等
例如`Conv::data_t`就是Conv中定义的data_t变量
```verilog
module ConvOperator(
    input clk,
    input rst,
    input Conv::data_vector kernel,
    input Conv::data_vector data,
    input in_valid,
    output reg in_ready,

    output Conv::result_t result,
    output reg out_valid,
    input out_ready
);
```
如果不希望反复使用`Conv::`来指示，可以用语句`import Conv::result_t`，将命名result_t引入该模块的命名空间，就可以直接用result_t了。
如果希望引入Conv内部的所有定义，可以使用`import Conv::*`

实际综合或仿真的时候，编译器对文件是逐个解析的，运气不好的话先解析了其他文件，就会出现Conv内容找不到的报错。
为了解决，我们将package做如下保护
```verilog
`ifndef __CONV_STRUCT__
`define __CONV_STRUCT__
package Conv;
    ...
endpackage
`endif
...
```
然后让需要使用这个包的文件include这个包
```verilog
`include"conv_struct.vh"
module ConvOperator(
    ...
```
这样当第一个使用 Conv 包的模块被解析的时候，Conv 包的数据会被插入到文件开头，确保 Conv 包被事先定义；当之后使用 Conv 包的模块被解析的时候，因为 `__CONV_STRUCT__` 已经被定义，所以之后不会再插入 Conv 包，防止重复定义

### 4 queue
仅用于仿真(sim)！不能在电路设计里写！！！
可以用`数据类型 变量名[$]`的方式声明一个队列，队列的每一项数据类型事先定义
```verilog
logic [3:0] myQueue[$]; 
Conv::data_t dataQueue[$] = {0,0,0,0};
```

可以用push_front, push_back, pop_front, pop_back分别对队列在队列开头插入一个值、在队列结尾插入一个值、在队列前弹出一个值、在队列末尾弹出一个值
此外可以用`dataQueue[i]`索引队列的第i个数据
```verilog
Conv::data_t dataQueue[$] = {0,0,0,0};
logic request;
always_ff@(posedge clk)begin
    if(in_valid & in_ready)begin
        request <= 1'b1;
        dataQueue.push_back(in_data);
        dataQueue.pop_front();
    end else begin
        request <= 1'b0;
    end
end
```
### 5 interface
两个模块之间不可避免地会有大量交互信号
`struct`中的信号只能同时作为一个模块的输入和输出，如果既有输入又有输出，就不太适用了
例如：
![[Pasted image 20250422100115.png|400]]
如果将所有的输入打包，所有的输出打包，会造成两个问题：
- 输入输出仍然有多个struct
- 不相关的数据打包在一起，语义混乱

为解决这些问题，SystemVerilog提出了`interface`语法
```verilog
interface Decoupled_ift #(
    parameter DATA_WIDTH = 64
);

    typedef logic [DATA_WIDTH-1:0] data_t;
    typedef logic ctrl_t;
    data_t data;
    ctrl_t valid;
    ctrl_t ready;

    modport Master(
        output data,
        output valid,
        input ready
    );

    modport Slave(
        input data,
        input valid,
        output ready
    );

endinterface
```
`interface`和一个普通的`module`之间不存在什么本质的区别
不同的地方在于，module内部的数据没办法访问，但interface可以用`.`访问内部参数变量，变量可以给任意外部变量赋值，如果这个变量没有在内部被赋值，则可以在外部被赋值

实例化`interface`：
```verilog
Decoupled_ift #(
    .DATA_WIDTH(64)
) data (

);

assign a = data.data;       // 获得 interface 内部变量的值
assign data.valid = 1'b1;   // 给 interface 内部变量赋值
assign c = data.DATA_WIDTH; // 获得 interface 内部参数的值
```
`interface`可以提供`modport`结构，它可以将内部数据组合为一组特定的输入输出端口
例子中的：
```verilog
modport Master(
    output data,
    output valid,
    input ready
);

modport Slave(
    input data,
    input valid,
    output ready
);
```

这样就可以将原来复杂的输入输出转换为：
```verilog
//原版
module A #(
    parameter DATA_WIDTH = 64   
)(
    input [DATA_WIDTH-1:0] data,
    input valid,
    output ready
);
...
endmodule


// 使用 interface 和 modport
module A #(
    parameter DATA_WIDTH = 64   
)(
    Decoupled_ift.Master data_information //接口，名称
);
```

模块的`modport`引脚也可以用`interface`的`modport`直接对应连接
```verilog
//原版
A #(
    .DATA_WIDTH(64)
) a (
    .data(data),
    .valid(valid),
    .ready(ready)
);


// 使用 interface 和 modport
A #(
    .DATA_WIDTH(64)
) a (
    .data(data.Master)
);
```

---
## Lab 4-2 串口使用
### uart串口传输协议
`uart(universal asynchronous receiver/transmitter)`，通用异步收发串口：一种低速、全双工、串行、异步数据传输设备
最简单的串口由两个引脚组成：
- `txd`：向外部设备发送数据
- `rxd`：接受外部设备发送的数据

uart串口数据线由两根线组成：
- 一根连接A设备的txd和B设备的rxd，将A的数据发送给B
- 一根连接A设备的rxd和B设备的txd，将B的数据发送给A
![[Pasted image 20250422092249.png|300]]

uart交互的双方不依赖时钟进行同步，所以是异步传输的
波特率(Baud Rate)用于描述串口传输的速率，例如波特率是9600，则每秒传输9600个bit的数据，或者说每个被传输的数据会在数据线上维持1/9600s
常见的波特率有9600、115200等

uart每次传输一个字节的数据，根据协议将数据打包为一个位序列，然后通过数据线发送出去，每个位保持1/baud s。
发送端：
- 不发送数据时，**高电平**
- 发送端需要发送数据时，先发送一个单位时间的**低电平**，表示发送开始
- 然后将待发送数据**从低到高**依次发送到数据线上，每个位保持**一个单位时间**
- 最后发送一个单位时间的**高电平**表示发送结束，回归高电平，知道下一次发送
接收端：
- 时刻检查电平，遇到低电平时发现有数据传输
- 每隔一个单位时间做一次采样，依次将8个bit的传输数据采样拼接
![[Pasted image 20250422092920.png|500]]
uart数据传输格式如下：
- 起始位：1 bit
- 数据位：8 bit
- 奇偶校验位：0/1 bit（可选）
- 终止位：1/1.5/2 bit（可选）

---
## Lab 5-1
### RISC-V 汇编语言
https://riscv.org/specifications/ratified/  (Volume 1, Unprivileged Specification)

sd
ld
lw
mv
j
jr
slli
add
addi
sub

### RISC-V调用约定
- 参数传递：通过寄存器传递，前几个参数通常存储在a0、a1、a2、a3等寄存器中，更多的参数则在栈中
- 返回值：返回值通常存储在a0寄存器中，返回值复杂的话也可以存储在栈中
- 寄存器的使用：被调用者要保存callee-saved寄存器，调用者可以使用caller-saved寄存器，但要在函数调用之前将这些寄存器的值保存下来
- 栈的使用：
	- 函数调用时，栈用于存储函数的参数、返回地址和callee-saved的值
	- 函数返回时，栈用于恢复调用者保存的寄存器和返回地址

### SPIKE模拟器和 RISCV PK
SPIKE 是一个基于 RISC-V ISA 模拟器，它可以让你在非 RISC-V 架构的机器上模拟执行 RISC-V 程序。

RISCV PK(RISC-V Proxy Kernel) 是一个基于 RISC-V ISA 的轻量级操作系统内核，它是一个开源项目，由 UC Berkeley 开发并维护。RISCV PK 旨在提供一个最小化的操作系统内核，以便在不同的 RISC-V 处理器上运行各种用户空间应用程序。
### 跳转表
跳转表是一种用于跳转到不同代码块的数据结构
通常由一组指针或地址组成，这些指针或地址指向不同的代码块
实现方法：数组/哈希表
用于替代大量的`if-else`语句或`switch`语句，提高执行效率

在RISC-V汇编中，跳转表通常使用`la(load address)`指令来加载地址，然后使用`jr(jump register)`或`jalr(jump and link register)`指令来进行跳转
### 冒泡排序
从数组第一个元素开始，依次比较相邻的两个元素，如果顺序错误就交换，把大的元素交换到数组末尾
每一轮找到最小的然后交换

## Lab 5-2
### QEMU(Quick EMUlator)
	开源的系统模拟器和虚拟机管理器
	能在不同的主机架构之间进行转换

基本参数：
- `-nographic`：不使用图形窗口，使用命令行
- `-machine`：指定要emulate的机器，可以通过命令`qemu-system-riscv64 -machine help`查看可选择的机器选项
- `-kernel`：指定内核image
- `-append cmdline`：使用cmdline作为内核的命令行
- `-device`：指定要模拟的设备，可以通过命令`qemu-system-riscv64 -device help`查看可选择的设备，通过命令`qemu-system-riscv64 -device <device_name>,help`查看某个设备的命令选项
- `-drive, file=<file_name>`：使用file_name作为文件系统
- `-S`：启动时暂停CPU执行
- `-s`：`-gdb tcp::1234`的简写
- `-bios default`：使用默认的OpenSBI firmware作为bootloader
### GDB(GNU Debugger)
	一个基于命令行的、功能强大的程序调试工具
	可以查看另一个程序在执行时实际在做什么
**功能：**
- 启动程序，并指定可能影响其行为的所有内容
- 使程序在指定条件下停止
- 检查程序停止时发生了什么
- 更改程序中的内容，纠正bug的影响

**基本命令：**
https://wizardforcel.gitbooks.io/100-gdb-tips/content/index.html
- `(gdb) layout asm`：显示汇编代码
- `(gdb) start`：单步执行，运行程序，停在第一执行语句
- `(gdb) continue`：从断点后继续执行，简写为`c`
- `(gdb) next`：单步调试(逐过程)，简写`n`
- `(gdb) step instruction`：执行单条指令，简写`si`
- `(gdb) run`：重新开始运行文件(`run-text`：加载文本文件， `run-bin`：加载二进制文件)，简写 `r`
- `(gdb) backtrace`：查看函数调用的栈帧和层级关系，简写`bt`
- `(gdb) break`：设置断点，简写`b`
	- 断在foo函数：`b foo`
	- 断在某地址：`b * 0x80200000`
- `(gdb) finish`：结束当前函数，返回到函数调用点
- `(gdb) frame`：切换函数的栈帧，简写`f`
- `(gdb) print`：打印值及地址，简写`p`
- `(gdb) info`：查看函数内部局部变量的数值，简写`i`
	- 查看寄存器ra的值：`i r ra`
- `(gdb) display`：追踪查看具体变量值
### Spike
	和qemu类似的指令模拟器
	对于RISCV程序的检查更为严谨

**基本参数：**
- - `-p<n>`: 模拟的 core 个数
- `m<a:m,b:n,...>`: 模拟的内存块和对应的内存范围
- `-d`: 交互调试模式
- `--log=<name>`: 将执行的中间 log 保存在文件中
- `-H`: 在等待调试器连接时不执行程序
- `--rbb-port=<port>`: 开放给调试器连接的调试端口

执行`Spike -d <可执行程序名>`就可以在 Spike 上直接执行 riscv 程序
**调试的基本命令：**
- (spike) 回车 : 单步执行一条汇编程序
- (spike) `help/h`: 查看调试命令介绍
- (spike) `reg <core> <reg>`: 查看指定 core 的通用寄存器的值，core 从 0 开始编号
- (spike) `freg <core> <reg>`: 查看指定 core 的浮点寄存器的值
- (spike) `pc <core>`: 查看指定 core 的当前 pc
- (spike) `mem [core] <hex addr>`: 查看指定 core 对应地址的内存
- (spike) `until pc <core> val`: 执行程序直到指定 core 的 pc 值为 val，类似于 gdb 打断点
- (spike) `quit/q`: 退出 Spike

### Openocd
	在RICV core调试的场景下，RISCV芯片为了方便硬件调试内部的信息会在内部集成一个debug module，并对外暴露一个JTAG接口。调试者可以用JTAG线的输入线向debug module输入命令，并用输出线得到需要读取的结果
	Spike 用于调试的 rbb-port 就是一个模拟的 JTAG 接口，但是我们无法直接用 gdb 对一个 JTAG 接口进行调试，openocd 起到一个中间桥梁的作用，它可以将 jtag 的数据协议和 gdb 的数据协议进行转换，从而实现 gdb 调试 riscv core 的目的

**基本参数**
-  `-f <path_to_config_file>`: 选择 OpenOCD 的配置文件路径。配置文件描述了目标板、调试接口和其他调试选项的设置，如提供的 spike.cfg
- `-c "<command>"}`: 指定要在 OpenOCD 启动时执行的命令，可以多次使用该参数以指定多个命令。
- `-s <path_to_scripts>`: 指定脚本文件路径，包含了一些常用的调试命令和配置选项。
- `-d <debug_level>`: 调试输出的级别，0（禁用调试输出）到 9（最详细的调试输出）之间的值。

## project：单周期CPU数据通路设计
### 单周期CPU
	在一个周期内完成一条指令
	时钟频率以执行最慢的那条指令为基准
	分成数据通路和控制单元两个模块
### 数据通路设计
#### Instruction Fetch (IF)：获取指令
- 从Instruction Memory中获取32位指令
- 更新当前的PC (Program Counter)
#### Instruction Decode (ID)：指令译码
- 对指令进行译码，提取信息，得到寄存器号、立即数、控制信号
- 译码完成后，通过Rs1、Rs2读取相应的寄存器值
#### EXecution (EX)：执行
- 执行一些运算操作，+-\*/，逻辑运算或左移右移等
- 跳转地址的计算也可以在这个阶段执行
#### MEMory access (MEM)：访存
- 从Data Memory中拿到数据
#### register Write Back (WB)：写回
- 把计算后获得的数据/从Data Memory读的数据写回寄存器中

![[Pasted image 20250507082311.png]]
#### 模块设计要点
- 示意图中的处理器为哈佛架构，内存分为 Instruction Memory 和 Data Memory。为了兼容输入样例，我们需要实现冯诺依曼架构。简便起见，我们将两块内存合并为一个双口内存，其中 0 号端口为提供给 IF 取指阶段的只读端口，1 号端口为提供给 MEM 阶段的读写端口，并且支持字节掩码
- Add 模块就是简单的加法，注意其和 ALU 的区别
- Registers 是 32 个 64 位寄存器
- 因为 RISC-V 的 Imm 在指令中的位置不是固定的，所以需要针对不同指令生成相应的 Imm，当然 Imm Gen 模块也可以放到之后要实现的控制单元模块中或者说指令解码器中
- ALU 模块根据要实现的指令实现具体的操作，注意要充分利用 Verilog 提供的操作符，例如 +, -, <<, >>> 等
#### 数字电路设计要点
- 在实验中， Instruction Memory 是组合电路，Data Memory 是时序电路（仅限于本实验，实际中并非如此）
- Registers、PC 模块使用时序电路
- ALU、Add 模块使用组合电路
- Imm Gen、Mux 模块使用组合电路